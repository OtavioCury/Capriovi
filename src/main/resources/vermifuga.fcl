FUNCTION_BLOCK vermifuga	// Definição do bloco

// Define variáveis de entrada
VAR_INPUT				
	FAMACHA : REAL;
	OPG : REAL;
	ECC : REAL;
END_VAR

// Define variáveis de saída
VAR_OUTPUT				
	NOTA : REAL; //(* RANGE(0 .. 10) *) 
END_VAR

// Define as funções de pertinencia para a variável FAMACHA
FUZZIFY FAMACHA		
	TERM bom := (0, 1)(2, 1)(3, 0);
	TERM  medio := (1.5,0) (3,1) (4.5,0);
	TERM ruim := (3, 0) (4, 1) (5,1);
END_FUZZIFY

// Define as funções de pertinencia para a variável ECC
FUZZIFY ECC			
	TERM ruim := (0, 1) (2, 1) (3.5, 0);
	TERM bom := (2, 0) (4,1) (5, 1);
END_FUZZIFY

// Define as funções de pertinencia para a variável OPG
FUZZIFY OPG			
	TERM bom := (0, 1) (500,0);
	TERM  medio := (300, 0) (700, 1) (800, 1) (1200, 0);
	TERM ruim := (1000, 0)(2000, 1);
END_FUZZIFY

// Define as funções de pertinencias para a variável de saída NOTA
DEFUZZIFY NOTA			
	TERM ruim := (0, 1) (2.5,1) (5,0);
	TERM media := (2.5,0) (4,1)(6,1)(7.5,0);
	TERM boa := (5,0) (7.5,1) (10,1);
	METHOD : COG;		// Método de centro de gravidade
	DEFAULT := 0;		// Valor padrão (caso nenhuma regra for ativada)
END_DEFUZZIFY

// Definição das regras
RULEBLOCK ConjuntoRegras


	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method
	
	//Para OPG bom
	RULE 1: IF OPG IS bom AND FAMACHA IS bom AND ECC IS ruim THEN NOTA IS boa;
	RULE 2: IF OPG IS bom AND FAMACHA IS bom AND ECC IS bom THEN NOTA IS boa;
	RULE 3: IF OPG IS bom AND FAMACHA IS medio AND ECC IS ruim THEN NOTA IS media;
	RULE 4: IF OPG IS bom AND FAMACHA IS medio AND ECC IS bom THEN NOTA IS  boa;
	RULE 5: IF OPG IS bom AND FAMACHA IS ruim AND ECC IS ruim THEN NOTA IS ruim;
	RULE 6: IF OPG IS bom AND FAMACHA IS ruim AND ECC IS bom THEN NOTA IS boa;
	
	//Para OPG medio
	RULE 7: IF OPG IS medio AND FAMACHA IS bom AND ECC IS ruim THEN NOTA IS media;
	RULE 8: IF OPG IS medio AND FAMACHA IS bom AND ECC IS bom THEN NOTA IS boa;
	RULE 9: IF OPG IS medio AND FAMACHA IS medio AND ECC IS ruim THEN NOTA IS ruim;
	RULE 10: IF OPG IS medio AND FAMACHA IS medio AND ECC IS bom THEN NOTA IS boa;
	RULE 11: IF OPG IS medio AND FAMACHA IS ruim AND ECC IS ruim THEN NOTA IS ruim;
	RULE 12: IF OPG IS medio AND FAMACHA IS ruim AND ECC IS bom THEN NOTA IS media;
	
	//Para OPG ruim
	RULE 13: IF OPG IS ruim AND FAMACHA IS bom AND ECC IS ruim THEN NOTA IS ruim;
	RULE 14: IF OPG IS ruim AND FAMACHA IS bom AND ECC IS bom THEN NOTA IS media;
	RULE 15: IF OPG IS ruim AND FAMACHA IS medio AND ECC IS ruim THEN NOTA IS ruim;
	RULE 16: IF OPG IS ruim AND FAMACHA IS medio AND ECC IS bom THEN NOTA IS media;
	RULE 17: IF OPG IS ruim AND FAMACHA IS ruim AND ECC IS ruim THEN NOTA IS ruim;
	RULE 18: IF OPG IS ruim AND FAMACHA IS ruim AND ECC IS bom THEN NOTA IS media;

END_RULEBLOCK

END_FUNCTION_BLOCK